/* *** Begin Furniture Factory *** */

item (FEAT_INDUSTRYTILES, ind_tile_furniture_factory_1) {
	property {
		substitute: 39;
		override: 39;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_furniture_factory_2) {
	property {
		substitute: 40;
		override: 40;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_furniture_factory_3) {
	property {
		substitute: 41;
		override: 41;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_furniture_factory_4) {
	property {
		substitute: 42;
		override: 42;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

/* Construction check for Furniture Factory */
switch (FEAT_INDUSTRIES, SELF, loc_check_furniture_factory, IsBeingFunded() || (IsNearTown() && TownHasSecondaryPopulation() && NoNearbyCompetitors(industry_furniture_factory, 128)))
	{1: return CB_RESULT_LOCATION_ALLOW; return CB_RESULT_LOCATION_DISALLOW;}

/* BEGIN PRODUCTION: Read up from bottom */

produce (produce_furniture_factory,
    [WDPR: LOAD_TEMP(1); COAT: LOAD_TEMP(1);], // consume
    [GOOD: LOAD_TEMP(1) * 2; PASS: LOAD_TEMP(2);], // produce
    0                                          // don't run callback again
)

/* If we don't have enough stored, produce as much as we can anyway. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_furniture_factory_min, [
    STORE_TEMP(min(incoming_cargo_waiting("WDPR"), incoming_cargo_waiting("COAT")), 1), // Set the production amount to the current input stockpile
    STORE_TEMP((((LOAD_TEMP(1) / 4) * transported_last_month_pct("GOOD")) / 100), 2), // Set the passenger production level
    ]) {produce_furniture_factory;}

/* Determine the production level and see if we have enough stored to meet it. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_furniture_factory_2, [
    STORE_TEMP(GetSecondaryProductionLevel(), 1), // Get production level
    STORE_TEMP((((LOAD_TEMP(1) / 4) * transported_last_month_pct("GOOD")) / 100), 2), // Set the passenger production level
    min(incoming_cargo_waiting("WDPR"), incoming_cargo_waiting("COAT")) > LOAD_TEMP(1), // Check if there's enough input cargo for this production level
    ]) {
        1: produce_furniture_factory;
        switch_produce_furniture_factory_min;
    }

/* If no cargo is waiting, bail out. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_furniture_factory, incoming_cargo_waiting("WDPR") > 0 && incoming_cargo_waiting("COAT") > 0)
    {
        1: switch_produce_furniture_factory_2;
        produce_none;
    }

/* END PRODUCTION */

item (FEAT_INDUSTRIES, industry_furniture_factory, 18) {
	property {
		substitute:	INDUSTRYTYPE_TEMPERATE_FACTORY;
		life_type: IND_LIFE_TYPE_PROCESSING;
		cargo_types: [
			accept_cargo("WDPR"),
			accept_cargo("COAT"),
			produce_cargo("GOOD", 0),
            produce_cargo("PASS", 0),
		];
		name: string(STR_NAME_FURNITURE_FACTORY);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_NAME_FURNITURE_FACTORY));
		prob_map_gen: 1;
		prob_in_game: 0;
		fund_cost_multiplier: 40;
		map_colour: 140;
	}
	graphics {
		extra_text_industry: StringGetSecondaryHelptext();
        location_check: loc_check_furniture_factory;
		produce_256_ticks: switch_produce_furniture_factory;
		colour: COLOUR_PURPLE;
		/* Don't allow any production changes */
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
	}
}
