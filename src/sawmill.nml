/* *** Begin Sawmill *** */

item (FEAT_INDUSTRYTILES, ind_tile_sawmill_1) {
	property {
		substitute: 11;
		override: 11;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_sawmill_2) {
	property {
		substitute: 12;
		override: 12;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_sawmill_3) {
	property {
		substitute: 13;
		override: 13;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_sawmill_4) {
	property {
		substitute: 14;
		override: 14;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_sawmill_5) {
	property {
		substitute: 15;
		override: 15;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

/* Construction check for Sawmill */
switch (FEAT_INDUSTRIES, SELF, loc_check_sawmill, IsBeingFunded() || (IsNearTown() && TownHasSecondaryPopulation() && NoNearbyCompetitors(industry_sawmill, 128)))
	{1: return CB_RESULT_LOCATION_ALLOW; return CB_RESULT_LOCATION_DISALLOW;}

/* BEGIN PRODUCTION: Read up from bottom */

produce (produce_sawmill,
    [WOOD: LOAD_TEMP(1); RFPR: LOAD_TEMP(1);], // consume
    [WDPR: LOAD_TEMP(1) * 2; PASS: LOAD_TEMP(2);], // produce
    0                                          // don't run callback again
)

/* If we don't have enough stored, produce as much as we can anyway. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_sawmill_min, [
    STORE_TEMP(min(incoming_cargo_waiting("WOOD"), incoming_cargo_waiting("RFPR")), 1), // Set the production amount to the current input stockpile
    STORE_TEMP((((LOAD_TEMP(1) / 4) * transported_last_month_pct("WDPR")) / 100), 2), // Set the passenger production level
    ]) {produce_sawmill;}

/* Determine the production level and see if we have enough stored to meet it. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_sawmill_2, [
    STORE_TEMP(GetSecondaryProductionLevel(), 1), // Get production level
    STORE_TEMP((((LOAD_TEMP(1) / 4) * transported_last_month_pct("WDPR")) / 100), 2), // Set the passenger production level
    min(incoming_cargo_waiting("WOOD"), incoming_cargo_waiting("RFPR")) > LOAD_TEMP(1), // Check if there's enough input cargo for this production level
    ]) {
        1: produce_sawmill;
        switch_produce_sawmill_min;
    }

/* If no cargo is waiting, bail out. */
switch (FEAT_INDUSTRIES, SELF, switch_produce_sawmill, incoming_cargo_waiting("WOOD") > 0 && incoming_cargo_waiting("RFPR") > 0)
    {
        1: switch_produce_sawmill_2;
        produce_none;
    }

/* END PRODUCTION */

item (FEAT_INDUSTRIES, industry_sawmill, 19) {
	property {
		substitute:	INDUSTRYTYPE_SAWMILL;
		life_type: IND_LIFE_TYPE_PROCESSING;
		cargo_types: [
			accept_cargo("WOOD"),
            accept_cargo("RFPR"),
            produce_cargo("WDPR", 0),
            produce_cargo("PASS", 0),
		];
		name: string(STR_NAME_SAWMILL);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_NAME_SAWMILL));
		prob_map_gen: 1;
		prob_in_game: 0;
		fund_cost_multiplier: 40;
		map_colour: 63;
	}
	graphics {
		extra_text_industry: StringGetSecondaryHelptext();
        location_check: loc_check_sawmill;
		produce_256_ticks: switch_produce_sawmill;
		/* Don't allow any production changes */
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
	}
}
