/* *** Begin Paper Mill *** */

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_1) {
	property {
		substitute: 64;
		override: 64;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_2) {
	property {
		substitute: 65;
		override: 65;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_3) {
	property {
		substitute: 66;
		override: 66;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_4) {
	property {
		substitute: 67;
		override: 67;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_5) {
	property {
		substitute: 68;
		override: 68;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_6) {
	property {
		substitute: 69;
		override: 69;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_7) {
	property {
		substitute: 70;
		override: 70;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_8) {
	property {
		substitute: 71;
		override: 71;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

/* Construction check for Paper Mill */
switch (FEAT_INDUSTRIES, SELF, loc_check_paper_mill, IsBeingFunded() || (IsNearTown() && TownHasSecondaryPopulation() && NoNearbyCompetitors(industry_paper_mill, 128)))
	{1: return CB_RESULT_LOCATION_ALLOW; return CB_RESULT_LOCATION_DISALLOW;}

/* BEGIN PRODUCTION: Read up from bottom */

produce (produce_paper_mill_efficient,
	[PASS: 4; WOOD: 8; RFPR: 4; KAOL: 4;],   // Consume
	[PAPR: 8;],                              // Produce
	1                                        // Run callback again
	)

produce (produce_paper_mill_improved_kaolin,
	[PASS: 4; WOOD: 8; KAOL: 4;],   // Consume
	[PAPR: 4;],                     // Produce
	1                               // Run callback again
	)

produce (produce_paper_mill_improved_chemicals,
	[PASS: 4; WOOD: 8; RFPR: 4;],   // Consume
	[PAPR: 4;],                     // Produce
	1                               // Run callback again
	)

produce (produce_paper_mill_basic,
	[PASS: 4; WOOD: 8;],    // Consume
	[PAPR: 2;],             // Produce
	1                       // Run callback again
	)

/* Is production super-boosted by Kaolin and Chemicals? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_super_boosted_kaolin, incoming_cargo_waiting("KAOL") >= 4) {
	1: produce_paper_mill_efficient;
	produce_paper_mill_improved_chemicals;
}

/* Is production boosted by Kaolin? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_boosted_kaolin, incoming_cargo_waiting("KAOL") >= 4) {
	1: produce_paper_mill_improved_kaolin;
	produce_paper_mill_basic;
}

/* Is production boosted by Chemicals? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_boosted_chemicals, incoming_cargo_waiting("RFPR") >= 4) {
	1: switch_produce_paper_mill_super_boosted_kaolin;
	switch_produce_paper_mill_boosted_kaolin;
}

/* Consume extra Wood*/
produce (produce_paper_mill_wood_stockpile,
	[WOOD: 1;],             // Consume
	[],                     // Produce
	1                       // Run callback again
	)

/* Consume extra Kaolin */
produce (produce_paper_mill_kaolin_stockpile,
	[KAOL: 1;],               // Consume
	[],                       // Produce
	1                         // Run callback again
	)

/* Consume extra Chemicals */
produce (produce_paper_mill_chemicals_stockpile,
	[RFPR: 1;],               // Consume
	[],                       // Produce
	1                         // Run callback again
	)

/* Should we consume extra Kaolin? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_idle_3, incoming_cargo_waiting("KAOL") > 4096) {
	1: produce_paper_mill_kaolin_stockpile;
	switch_produce_idle;
}

/* Should we consume extra Chemicals? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_idle_2, incoming_cargo_waiting("RFPR") > 4096) {
	1: produce_paper_mill_chemicals_stockpile;
	switch_produce_paper_mill_idle_3;
}

/* Should we consume extra Wood? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill_idle, incoming_cargo_waiting("WOOD") > 4096) {
	1: produce_paper_mill_wood_stockpile;
	switch_produce_paper_mill_idle_2;
}

/* Check if we have both Workers and Wood */
switch (FEAT_INDUSTRIES, SELF, switch_produce_paper_mill, (incoming_cargo_waiting("PASS") >= 4 && incoming_cargo_waiting("WOOD") >= 8)) {
	1: switch_produce_paper_mill_boosted_chemicals;
	switch_produce_paper_mill_idle;
}

/* END PRODUCTION */

item (FEAT_INDUSTRIES, industry_paper_mill, 14) {
	property {
		substitute:	INDUSTRYTYPE_PAPER_MILL;
		life_type: IND_LIFE_TYPE_PROCESSING;
		cargo_types: [
			accept_cargo("PASS"),
			accept_cargo("WOOD"),
			accept_cargo("KAOL"),
			accept_cargo("RFPR"),
			produce_cargo("PAPR", 0),
		];
		name: string(STR_NAME_PAPER_MILL);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_NAME_PAPER_MILL));
		spec_flags: bitmask(IND_FLAG_LONG_CARGO_TYPE_LISTS);
		prob_map_gen: 1;
		prob_in_game: 0;
		fund_cost_multiplier: 40;
	}
	graphics {
		location_check: loc_check_paper_mill;
		produce_cargo_arrival: switch_produce_paper_mill;
		extra_text_industry: string(STR_PAPER_MILL_HELPTEXT);
		/* Don't allow any production changes */
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
	}
}
