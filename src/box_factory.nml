/* *** Begin Box Factory *** */

item (FEAT_INDUSTRYTILES, ind_tile_box_factory_1) {
	property {
		substitute: 43;
		override: 43;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_box_factory_2) {
	property {
		substitute: 44;
		override: 44;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_box_factory_3) {
	property {
		substitute: 45;
		override: 45;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_box_factory_4) {
	property {
		substitute: 46;
		override: 46;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

/* Construction check for Box Factory */
switch (FEAT_INDUSTRIES, SELF, loc_check_box_factory, IsBeingFunded() || (IsNearTown() && TownHasSecondaryPopulation() && NoNearbyCompetitors(industry_box_factory, 128)))
	{1: return CB_RESULT_LOCATION_ALLOW; return CB_RESULT_LOCATION_DISALLOW;}

/* BEGIN PRODUCTION: Read up from bottom */

produce (produce_box_factory,
	[PASS: 1; PAPR: 1;],	// Consume
	[MNSP: 1;],				// Produce
	1 						// Run callback again
	)

produce (produce_box_factory_stockpile,
	[PAPR: 1;],               // Consume
	[],                       // Produce
	1                         // Run callback again
	)

/* We don't have both Workers and Paper. Which do we consume? */
switch (FEAT_INDUSTRIES, SELF, switch_produce_box_factory_idle, incoming_cargo_waiting("PAPR") > 4096) {
	1: produce_box_factory_stockpile;
	switch_produce_idle;
}

/* Check if we have both Workers and Paper */

switch (FEAT_INDUSTRIES, SELF, switch_produce_box_factory, (incoming_cargo_waiting("PASS") > 0 && incoming_cargo_waiting("PAPR") > 0)) {
	1: produce_box_factory;
	switch_produce_box_factory_idle;
}

/* END PRODUCTION */

item (FEAT_INDUSTRIES, industry_box_factory, 7) {
	property {
		substitute:	INDUSTRYTYPE_PRINTING_WORKS;
		life_type: IND_LIFE_TYPE_PROCESSING;
		cargo_types: [
			accept_cargo("PASS"),
			accept_cargo("PAPR", produce_cargo("MNSP", 0.5)),
		];
		name: string(STR_NAME_BOX_FACTORY);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_NAME_BOX_FACTORY));
		prob_map_gen: 1;
		prob_in_game: 0;
		fund_cost_multiplier: 40;
		map_colour: 95;
	}
	graphics {
		location_check: loc_check_box_factory;
		produce_cargo_arrival: switch_produce_box_factory;
		colour: COLOUR_DARK_GREEN;
		/* Don't allow any production changes */
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
	}
}
